
&НаКлиенте
&ИзменениеИКонтроль("ОбработатьШтрихкоды")
Процедура lazmakОбработатьШтрихкоды(ДанныеШтрихкодов)

	#Удаление
	Если Не ШтрихкодированиеНоменклатурыКлиент.ШтрихкодыВалидны(ДанныеШтрихкодов) Тогда
		Возврат;
	КонецЕсли;
	#КонецУдаления
	
	#Вставка
	ПовторныйПоиск = Ложь;
	Если ТипЗнч(ДанныеШтрихкодов) = Тип("Структура") И Не ДанныеШтрихкодов.Свойство("ПовторныйПоиск", ПовторныйПоиск) Тогда
		ПовторныйПоиск = Ложь;
	КонецЕсли;
	
	ДанныеШтрихкодов = ШтрихкодированиеНоменклатурыКлиент.ШтрихкодИзDataMatrix(ДанныеШтрихкодов);
	Если ДанныеШтрихкодов = Неопределено Тогда
		Возврат;
	КонецЕсли;
	#КонецВставки

	ИзменятьКоличество = Не (СостояниеРедактирования = "НеРедактируется"); 

	СтруктураДействийСДобавленнымиСтроками = Новый Структура;
	СтруктураДействийСДобавленнымиСтроками.Вставить("ЗаполнитьПризнакАртикул", Новый Структура("Номенклатура", "Артикул"));
	СтруктураДействийСДобавленнымиСтроками.Вставить("ЗаполнитьПризнакТипНоменклатуры", Новый Структура("Номенклатура", "ТипНоменклатуры"));
	СтруктураДействийСДобавленнымиСтроками.Вставить("ПроверитьСериюРассчитатьСтатус", Новый Структура("Склад, ПараметрыУказанияСерий", Объект.Склад, ПараметрыУказанияСерий));
	СтруктураДействийСДобавленнымиСтроками.Вставить("ЗаполнитьПризнакТипИзмеряемойВеличины", Новый Структура("Номенклатура", "ТипИзмеряемойВеличины"));
	ДобавитьВСтруктуруДействияПриИзмененииКоличестваУпаковокФакт(СтруктураДействийСДобавленнымиСтроками);

	Если СостояниеРедактирования = "ОграниченноеРедактирование" Тогда
		СтруктураДействийСДобавленнымиСтроками.Вставить("ЗаполнитьФлагИзлишекПорча");
	КонецЕсли;

	СтруктураДействийСИзмененнымиСтроками = Новый Структура;
	ДобавитьВСтруктуруДействияПриИзмененииКоличестваУпаковокФакт(СтруктураДействийСИзмененнымиСтроками);

	СтруктураДействий = ШтрихкодированиеНоменклатурыКлиент.ПараметрыОбработкиШтрихкодов();

	СтруктураДействий.Штрихкоды                              = ДанныеШтрихкодов;
	СтруктураДействий.СтруктураДействийСДобавленнымиСтроками = СтруктураДействийСДобавленнымиСтроками;
	СтруктураДействий.СтруктураДействийСИзмененнымиСтроками  = СтруктураДействийСИзмененнымиСтроками;
	СтруктураДействий.ПараметрыУказанияСерий                 = ПараметрыУказанияСерий;
	СтруктураДействий.ИмяКолонкиКоличество                   = "КоличествоУпаковокФакт";
	СтруктураДействий.НеИспользоватьУпаковки                 = Не ИспользоватьАдресноеХранение;
	СтруктураДействий.ИзменятьКоличество                     = ИзменятьКоличество;
	СтруктураДействий.ТолькоТовары                           = Истина;

	ОбработатьШтрихкодыСервер(СтруктураДействий,КэшированныеЗначения);

	#Вставка
	Если СтруктураДействий.НеизвестныеШтрихкоды.Количество() > 0 И НЕ ПовторныйПоиск Тогда
		
		СтруктураШтрихкод = СтруктураДействий.НеизвестныеШтрихкоды[0];
		Если Лев(СтруктураШтрихкод.Штрихкод, 1) = "0" И СтрДлина(СтруктураШтрихкод.Штрихкод) > 13 Тогда
			СтрШтрихкод = Прав(СтруктураШтрихкод.Штрихкод, 13);
		Иначе
			СтрШтрихкод = СтруктураШтрихкод.Штрихкод;
		КонецЕсли;
		
		lazmakОбработатьШтрихкоды(Новый Структура("Штрихкод, Количество, ПовторныйПоиск", 
			СтрШтрихкод, 1, Истина));
		СтруктураДействий.НеизвестныеШтрихкоды.Очистить();
		
	КонецЕсли;
	#КонецВставки
	
	ШтрихкодированиеНоменклатурыКлиент.ОбработатьНеизвестныеШтрихкоды(СтруктураДействий,КэшированныеЗначения,ЭтаФорма);

	Если ШтрихкодированиеНоменклатурыКлиент.НужноОткрытьФормуУказанияСерийПослеОбработкиШтрихкодов(СтруктураДействий) Тогда

		ТекущиеДанныеИдентификатор = СтруктураДействий.МассивСтрокССериями[0];

		ПодключитьОбработчикОжидания("ОткрытьПодборСерийПриСканированииШтрихкодаНоменклатуры",0.1,Истина);

	КонецЕсли;

	Если СтруктураДействий.ТекущаяСтрока <> Неопределено Тогда
		Элементы.Товары.ТекущаяСтрока = СтруктураДействий.ТекущаяСтрока;
	КонецЕсли;

КонецПроцедуры
